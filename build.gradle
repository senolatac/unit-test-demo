plugins {
	id 'org.springframework.boot' version '2.5.1'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'io.freefair.lombok' version '5.3.3.3'
	id 'java'
}

subprojects {
    group = 'co.example' // our domain group
    version = '1.0' // we don't use versioning so it can be any semver

    repositories {
        mavenLocal() // first try local repo
        mavenCentral()
    }

    apply plugin: 'java' // just java plugin
    apply plugin: 'java-library' // for api() to work in gradle files; which replaces compile()
    apply plugin: 'io.freefair.lombok'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management' // auto dependency resolutions for boot

    sourceCompatibility = '11'
    targetCompatibility = '11'

    // disable default lombok config generation
    generateLombokConfig.enabled = false

    dependencies {
        testImplementation('org.springframework.boot:spring-boot-starter-test')
        testImplementation('com.h2database:h2')
    }

    test {
        useJUnitPlatform()
    }

    tasks.withType(JavaCompile) { // generic encoding to prevent some problems
        options.encoding = 'UTF-8'
    }
}
